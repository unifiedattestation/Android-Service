plugins {
    id 'com.android.application'
}

android {
    namespace 'net.uattest.service'
    compileSdk 34

    def keystorePath = System.getenv("ANDROID_KEYSTORE_PATH")
    def keystorePassword = System.getenv("ANDROID_KEYSTORE_PASSWORD")
    def keyAliasValue = System.getenv("ANDROID_KEY_ALIAS")
    def keyPasswordValue = System.getenv("ANDROID_KEY_PASSWORD")

    if (keystorePath) {
        signingConfigs {
            release {
                storeFile file(keystorePath)
                storePassword keystorePassword
                keyAlias keyAliasValue
                keyPassword keyPasswordValue
            }
        }
    }

    defaultConfig {
        applicationId "net.uattest.service"
        minSdk 28
        targetSdk 34
        versionCode 1
        versionName "1.0"

    }

    buildTypes {
        release {
            if (keystorePath) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        aidl true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all { output ->
                def vName = variant.versionName ?: "0.0.0"
                def vCode = variant.versionCode ?: 0
                outputFileName = "ua-android-service-${vName}(${vCode}).apk"
            }
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'net.uattest:android-sdk:0.1.0'
}
